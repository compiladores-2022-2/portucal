
PROGRAMA ::= DEFINICOES DECLARACOES NOVO_BLOCO
 
DEFINICOES ::= "const" "{" DEFINICAO LISTA_DEFINICOES_OU_VAZIO "}"
DEFINICOES ::= ''

LISTA_DEFINICOES_OU_VAZIO ::= ";" DEFINICAO LISTA_DEFINICOES_OU_VAZIO 
LISTA_DEFINICOES_OU_VAZIO ::= ''

DEFINICAO ::= "ID" "=" LITERAL
 
DECLARACOES ::= "declrs" "{" DECLARACAO LISTA_DECLARACOES_OU_VAZIO "}" 
DECLARACOES ::= ''

LISTA_DECLARACOES_OU_VAZIO ::= ";" LISTA_DECLARACOES_OU_VAZIO
LISTA_DECLARACOES_OU_VAZIO ::= ''

DECLARACAO ::= DEC_VAR
DECLARACAO ::= DEC_PROC
DECLARACAO ::= DEC_FUNC
DECLARACAO ::= DEC_TIPO

DEC_VAR ::= "var" "id" IDS_OU_VAZIO ":" TIPO

IDS_OU_VAZIO ::= "," "id" IDS_OU_VAZIO
IDS_OU_VAZIO ::= ''
 
LISTA_PARAMETROS_OU_VAZIO ::= "," PARAMETRO LISTA_PARAMETROS_OU_VAZIO
LISTA_PARAMETROS_OU_VAZIO ::= ''

TALVEZ_REF ::= "&"
TALVEZ_REF ::= ''

PARAMETRO ::= TALVEZ_REF "id" ":" TIPO

PARAMETROS_OU_VAZIO ::= PARAMETRO LISTA_PARAMETROS_OU_VAZIO
PARAMETROS_OU_VAZIO ::= ''
 
DEC_PROC ::= "proc" "id" "(" PARAMETROS_OU_VAZIO ")" BLOCO

DEC_FUNC ::= "func" "id" "(" PARAMETROS_OU_VAZIO ")" ":" TIPO BLOCO
 
DEC_TIPO ::= "tipo" "id" "=" TIPO
 
TIPO ::= TIPO_PREF COLCHETES_OU_VAZIO

COLCHETES_OU_VAZIO ::= "[" C_EXPR "]" COLCHETES_OU_VAZIO
COLCHETES_OU_VAZIO ::= ''

TIPO_PREF ::= "id"
TIPO_PREF ::= TIPO_SIMPLES
TIPO_PREF ::= REGISTRO

TIPO_SIMPLES ::= "int"
TIPO_SIMPLES ::= "logico"
TIPO_SIMPLES ::= "flut"
TIPO_SIMPLES ::= "carac"

REGISTRO ::= "estrutura" "{" DEC_VAR LISTA_DEC_VAR "}"

LISTA_DEC_VAR ::= ";" LISTA_DEC_VAR
LISTA_DEC_VAR ::= ''
  
NOVO_BLOCO ::= "bloco" BLOCO

BLOCO ::= "{" DECLARACOES COMANDO LISTA_COMANDOS_OU_VAZIO "}"

LISTA_COMANDOS_OU_VAZIO ::= ";" COMANDO LISTA_COMANDOS_OU_VAZIO
LISTA_COMANDOS_OU_VAZIO ::= ''
 
COMANDO ::= "continue"
COMANDO ::= "pare"
COMANDO ::= RETORNO
COMANDO ::= NOVO_BLOCO
COMANDO ::= CONTROLE
COMANDO ::= REPETICAO
COMANDO ::= ATRIBUICAO
COMANDO ::= "escreva" "(" EXPR LISTA_EXPR_OU_VAZIO ")"
COMANDO ::= "leia" "(" "id" MODIFICADORES_OU_VAZIO ")"

LISTA_EXPR_OU_VAZIO ::= "," EXPR LISTA_EXPR_OU_VAZIO
LISTA_EXPR_OU_VAZIO ::= ''

CONTROLE ::= SE
CONTROLE ::= ESCOLHA

SE ::= "se" "(" EXPR ")" BLOCO SENAO_OU_VAZIO

SENAO_OU_VAZIO ::= "senao" BLOCO_OU_SE
SENAO_OU_VAZIO ::= ''

BLOCO_OU_SE ::= BLOCO
BLOCO_OU_SE ::= SE
 
ESCOLHA ::= "escolha" "(" EXPR ")" "{" CASOS_ESCOLHA "}"

CASOS_ESCOLHA ::= CASO_ESCOLHA CASO_ESCOLHA_P
CASOS_ESCOLHA ::= ESCOLHA_PADRAO

CASO_ESCOLHA_P ::= CASOS_ESCOLHA
CASO_ESCOLHA_P ::= ''

CASO_ESCOLHA ::=  "caso" C_EXPR ":" COMANDO ";"

ESCOLHA_PADRAO ::= "padrao" ":" COMANDO ";"
 
REPETICAO ::= ENQUANTO
REPETICAO ::= FACA_ENQUANTO
REPETICAO ::= PARA

ENQUANTO ::= "enquanto" "(" EXPR ")" BLOCO

FACA_ENQUANTO ::= "faca" BLOCO "enquanto" "(" EXPR ")"

PARA ::= "para" "(" ATRIBUICAO ";" EXPR ";" ATRIBUICAO ")" BLOCO
 
ATRIBUICAO ::= "id" MODIFICADORES_OU_VAZIO "=" EXPR
 
RETORNO ::= "retorne" EXPR_OU_VAZIO

EXPR_OU_VAZIO ::= EXPR
EXPR_OU_VAZIO ::= ''
 
EXPR ::= T_OR EXPR_P 

EXPR_P ::= ou T_OR EXPR_P
EXPR_P ::= ''

T_OR ::= T_AND T_OR_P 

T_OR_P ::= e T_AND T_OR_P
T_OR_P ::= ''

T_AND ::= T_REL T_AND_P 

T_AND_P ::= "==" T_REL T_AND_P
T_AND_P ::= "!=" T_REL T_AND_P
T_AND_P ::= "<=" T_REL T_AND_P
T_AND_P ::= ">=" T_REL T_AND_P
T_AND_P ::= "<" T_REL T_AND_P
T_AND_P ::= ">" T_REL T_AND_P
T_AND_P ::= ''

T_REL ::= T_ARIT T_REL_P

T_REL_P ::= "+" T_ARIT T_REL_P
T_REL_P ::= "-" T_ARIT T_REL_P
T_REL_P ::= ''

T_ARIT ::= T_FATOR T_ARIT_P

T_ARIT_P ::= "*" T_FATOR T_ARIT_P
T_ARIT_P ::= "/" T_FATOR T_ARIT_P
T_ARIT_P ::= "mod" T_FATOR T_ARIT_P
T_ARIT_P ::= ''

T_FATOR ::= T_EXPO T_FATOR_P

T_FATOR_P ::= "^" T_FATOR
T_FATOR_P ::= ''

T_EXPO ::= T_UN

T_UN ::= "+" T_UN
T_UN ::= "-" T_UN
T_UN ::= "!" T_UN
T_UN ::= T_PAR_ID
T_UN ::= CAST T_UN

T_PAR_ID ::= "(" EXPR ")"
T_PAR_ID ::= EXPR_FOLHA 
 
CAST ::= "%" TIPO "%"
 
EXPR_FOLHA ::= LITERAL
EXPR_FOLHA ::= "id" CHAMADA_FUNC_OU_VAZIO MODIFICADORES_OU_VAZIO
 
MOD_TYPE ::= "." "id"
MOD_TYPE ::= "[" EXPR "]"

MODIFICADORES_OU_VAZIO ::= MOD_TYPE MODIFICADORES_OU_VAZIO
MODIFICADORES_OU_VAZIO ::= ''
 
CHAMADA_FUNC_OU_VAZIO ::= "(" ARGS_FUNC_OU_VAZIO ")"
CHAMADA_FUNC_OU_VAZIO ::= ''
 
ARGS_FUNC_OU_VAZIO ::= ''
ARGS_FUNC_OU_VAZIO ::= EXPR LISTA_EXPR_OU_VAZIO 
 
LITERAL ::= LITERAL_LOGICO
LITERAL ::= LITERAL_CARAC
LITERAL ::= LITERAL_INT
LITERAL ::= LITERAL_FLUT
LITERAL ::= LITERAL_LISTA

LITERAL_LOGICO ::= "verdadeiro"
LITERAL_LOGICO ::= "falso"

LITERAL_CARAC ::= "CHAR"

LITERAL_INT ::= "NUMERO_INTEIRO"

LITERAL_FLUT ::= "NUMERO_FLUT"

LITERAL_LISTA ::= "[" LITERAL LISTA_LITERAL_OU_VAZIO "]"
LISTA_LITERAL_OU_VAZIO ::= "," LITERAL LISTA_LITERAL_OU_VAZIO
LISTA_LITERAL_OU_VAZIO ::= ''
 
C_EXPR ::= C_T_OR C_EXPR_P 

C_EXPR_P ::= ou C_T_OR C_EXPR_P
C_EXPR_P ::= ''

C_T_OR ::= C_T_AND C_T_OR_P 

C_T_OR_P ::= e C_T_AND C_T_OR_P
C_T_OR_P ::= ''

C_T_AND ::= C_T_REL C_T_AND_P 

C_T_AND_P ::= "==" C_T_REL C_T_AND_P
C_T_AND_P ::= "!=" C_T_REL C_T_AND_P
C_T_AND_P ::= "<=" C_T_REL C_T_AND_P
C_T_AND_P ::= ">=" C_T_REL C_T_AND_P
C_T_AND_P ::= "<" C_T_REL C_T_AND_P
C_T_AND_P ::= ">" C_T_REL C_T_AND_P
C_T_AND_P ::= ''

C_T_REL ::= C_T_ARIT C_T_REL_P

C_T_REL_P ::= "+" C_T_ARIT C_T_REL_P
C_T_REL_P ::= "-" C_T_ARIT C_T_REL_P
C_T_REL_P ::= ''

C_T_ARIT ::= C_T_FATOR C_T_ARIC_T_P

C_T_ARIC_T_P ::= "*" C_T_FATOR C_T_ARIC_T_P
C_T_ARIC_T_P ::= "/" C_T_FATOR C_T_ARIC_T_P
C_T_ARIC_T_P ::= "mod" C_T_FATOR C_T_ARIC_T_P
C_T_ARIC_T_P ::= ''

C_T_FATOR ::= C_T_EXPO C_T_FATOR_P

C_T_FATOR_P ::= "^" C_T_FATOR
C_T_FATOR_P ::= ''

C_T_EXPO ::= C_T_UN

C_T_UN ::= "+" C_T_UN
C_T_UN ::= "-" C_T_UN
C_T_UN ::= "!" C_T_UN
C_T_UN ::= C_T_PAR_ID
C_T_UN ::= CAST C_T_UN

C_T_PAR_ID ::= "(" C_EXPR ")"
C_T_PAR_ID ::= EXPR_FOLHA 